import "pkg:/source/core/Utils.bs"
import "pkg:/source/ml/cells/BaseCell.bs"

@node("PosterCellEfficient", "ml_BaseCell")
class PosterCellEfficient extends ml.BaseCell

  private poster as roSGnode
  private label as roSGnode

  function new()
    super()
  end function

  protected override function createViews() as void
    ' ? " CREATE VIEWS "
    m.poster = mc.createSGNode("mv_Poster", m.top, "poster", {
      translation: [0, 0]
      loadingBitmapUri: "pkg:/images/backgrounds/button-square-bg.9.png"
      failedBitmapUri: "pkg:/images/backgrounds/button-square-bg.9.png"
      uri: ""
      loadWidth: 400
      loadHeight: 250
      size: [400, 250]
      scaleRotateCenter: [400 / 2, 250 / 2]
      loadDisplayMode: "zoomToFill"
    })
    mc.createSGNode("mv_Rectangle", m.top, "rectangle", {
      opacity: 0.4
      color: "#000000"
      size: [400, 50]
      translation: [0, 100]
    })
    m.label = mc.createSGNode("mv_Label", m.top, "label", {
      size: [400, 50]
      fontKey: "medium,30"
      color: "#ffffff"
      horizAlign: "center"
      vertAlign: "center"
      translation: [10, 100]
    })

  end function

  protected override function cleanup()
    ' ? "CLEANUP"
  end function

  protected override function onShowWithContent(content as roSGnode)
    ' ? ">>  SHOW  onShowWithContent"
    m.setFocusState(1, false)
    m.poster.uri = m.content.HDGRIDPOSTERURL
    m.label.text = `${m.index} ${content.title}`
  end function

  protected override function onHide()
    ' ? ">> HIDE"
    m.poster.uri = ""'m.poster.loadingBitmapUri
  end function

  private function onDidGainFocus(event as ml.ListEvent)
    ' ? " OCDGF " direction m.index
    m.setFocusState(1, true)
  end function

  private function onDidLoseFocus(event as ml.ListEvent)
    ' ? " OCDLF " direction m.index
    m.setFocusState(1, false)
  end function

  private function onGainingFocus(event as ml.ListEvent)
    ' ? " OCGF " direction fraction
    m.setFocusState(event.fraction, true)
  end function

  private function onLosingFocus(event as ml.ListEvent)
    ' ? " OCLF " direction fraction
    m.setFocusState(event.fraction, false)
  end function

  private function setFocusState(fraction as float, isGaining as boolean) as void
    ' ? " SFS " isGaining fraction m.index

    if fraction > 1
      fraction = 1
    end if
    if isGaining
      scale = 1 + (0.1 * fraction)
    else
      scale = 1 + (0.1 - (0.1 * fraction))
    end if
    m.poster.scale = [scale, scale]
  end function
end class
