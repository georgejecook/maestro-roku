import "pkg:/source/ml/ListItemScroller.bs"
import "pkg:/source/ml/ListItemViewManager.bs"
import "pkg:/source/core/Tasks.bs"
import "pkg:/source/ml/ListMixin.bs"
import "pkg:/source/view/BaseView.bs"
import "pkg:/source/view/Animation.bs"
import "pkg:/source/ml/FocusIndicatorConfig.bs"
import "pkg:/source/ml/ListContentManager.bs"

namespace ml
  @node("ml_List", "mv_BaseView")
  class List extends mv.BaseView

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ Content
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    'must gbe of type ListContent
    ' @observer("onContentChange")
    public content as mc.types.node

    'view that will respond to events
    public delegate as mc.types.node

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ View State
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    @alwaysnotify
    public state = "none"

    public width = 1920
    public height = 1080

    @alwaysnotify
    @observer("onIsHiddenChange")
    public isHidden = false

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ Debug Flags
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    @observer("onUpdateNow")
    public _updateNow = false
    public _forceReloadListWhenPressingOption = false

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ state
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    public isScrolling = false
    public screenOffset = 0

    public scrollDirection = 0

    public focusedRowIndex = 0
    public focusedSubRowIndex = 0

    public focusedCellIndex = 0
    public focusedfraction = 0.0

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ Views
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    private contentGroup as mc.types.node
    private rowsGroup as mc.types.node

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ Shared managers
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    public focusIndicatorManager as mc.types.node

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ internal event tracking
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    public nextFocusIndex as mc.types.array

    public screenPos = 0
    public nextScreenPos = 0

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ managers and internal state
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    private viewManager as ml.ListItemViewManager
    private itemScroller as ml.ListItemScroller
    private contentManager as ml.ListContentManager
    private sourceFocusRect as object
    private targetFocusRect as object
    private isInitialized = false
    private isDoingFlowFocus = false

    private preHiddenContent as mc.types.node


    private keyDirections = {
      "rewind": -1
      "fastforward": 1
      "left": -1
      "right": 1
      "up": -1
      "down": 1
    }

    private rowKeys = {
      "rewind": true
      "fastforward": true
      "left": true
      "right": true
    }

    private listKeys = {
      "up": true
      "down": true
    }

    function new()
      super("ml_List")
    end function

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ initialization
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    protected override function initialize()
      super.initialize()
      m.createViews()
      m.initManagers()
      m.isInitialized = true
      ' m.onContentChange()
    end function

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ Private Methods
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    private function createViews()
      'TODO - might still end up needing this... depends on how floating focus lands
      m.contentGroup = m.createSGNode("Group", m.top, "contentGroup")
      m.rowsGroup = m.createSGNode("Group", m.contentGroup, "rowsGroup")
      m.focusIndicatorManager = m.createSGNode("ml_FocusIndicatorManager", m.top, "focusIndicator", {
        list: m.top
      })

    end function

    private function initManagers()
      m.rowsGroup = m.top.findNode("rowsGroup")

      m.viewManager = new ml.ListItemViewManager(m.top, m.rowsGroup, 0, m.height, m.global.componentPool)

      m.itemScroller = new ml.ListItemScroller(m.viewManager, m.focusIndicatorManager)
      m.itemScroller.offsetContainer = m.contentGroup
      m.contentManager = new ml.ListContentManager(m, m.viewManager, m.itemScroller, m.focusIndicatorManager)
    end function

    private function createEventsBroker(id as string)
      node = createObject("roSGNode", "Node")
      node.id = id
      node.addFields({ "listEvent": {}, "viewportEvents": [] })
      return node
    end function

    private wasFocused = false

    private function resetState()
      m.log.info("resetState")

      m.wasFocused = m.top.isFocused or m.top.isChildFocused
      m.top.visible = false
      if m.contentManager.content <> invalid
        m.log.info("new content is present")
        m.viewManager.setContent(m.contentManager.content)
        m.top.clippingrect = [0, 0, m.width, m.height]
      else
        m.log.info("content is being set to invalid - resetting view manager")
        m.viewManager.reset()
      end if
      ' m.focusIndicatorManager.isHidden = not m.top.isFocused and not m.top.isChildFocused
      if m.top.state = "rendered"
        m.onListRenderState("rendered")
      else
        m.observeNodeField(m.top, "state", m.onListRenderState, "value", true)
      end if

    end function

    private function onListRenderState(state as string)
      m.log.info("onListRenderState", state)
      if state = "rendered"
        'FIXME: WHAT IS THIS?!
        if m.top.isFocused or m.top.isChildFocused
          m.top.focusIndicatorManager.isHidden = false
        end if
        m.moveToRowItemIndex(m.focusedRowIndex, m.focusedCellIndex, false)
        m.top.visible = true
      end if
    end function

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ callbacks
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    function setContent(content as mc.types.node) as void
      m.contentManager.setContent(content)
    end function

    function synchronizeContent()
      m.contentManager.synchronizeContent()
    end function

    private function cleanup()
      m.log.verbose(" CLEANING UP LIST")
      m.viewManager.reset()
      m.focusIndicatorManager@.setNextFocusIndicatorConfig(invalid)
    end function

    private function onIsHiddenChange()
      m.log.info("hiding the list")

      m.itemScroller.notifyViewportEvents(m.itemScroller.direction, true)
    end function

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ Public Methods
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    function onUpdateNow()
      m.log.info("force updating list")
      m.forceUpdate(true)
    end function

    function forceUpdate(fullUpdate = false as boolean) as void
      ' cellInfo = m.itemScroller.getCellInfoForContentIndex(m.focusedRowIndex)
      if m.top.state = "rendering"
        m.log.verbose("cannot forceUpdate list while rendering is taking place")
        return
      end if

      m.log.verbose(m.focusedRowIndex, " ", m.focusedCellIndex)
      m.log.verbose(" NC ", m.viewManager.numberOfItems, " I ", m.itemScroller.index)
      m.viewManager.forceUpdateLayout(fullUpdate = true)
      'Note - this will result in a double call; but that's what it's gonna take to be sure things are okay
      m.itemScroller.notifyViewportEvents(m.itemScroller.direction, true)

      ' ? " NC " ; m.viewManager.numberOfItems ; " I " ; m.itemScroller.index
      if m.viewManager.numberOfItems > 0
        m.focusIndicatorManager.isHidden = true
        m.log.verbose("LIST HAS COMPONENTS ", m.focusedRowIndex, " ", m.focusedCellIndex)
        ' m.moveToRowItemIndex(m.focusedRowIndex, m.focusedCellIndex, false)
        ' m.moveToRowItemIndex(0, 0, true)
        m.moveToRowItemIndex(m.focusedRowIndex, m.focusedCellIndex, false)
        'correct the focus if the item has changed.

        ' if m.itemScroller.index >= m.viewManager.numberOfItems
        '   index = mc.clamp(m.itemScroller.index, 0, m.viewManager.numberOfItems - 1)
        '   if index >= 0
        '    ' ? "CORRECTING INDEX"; index
        '     m.moveToRowItemIndex(m.focusedRowIndex, m.focusedCellIndex, false)
        '   end if
        ' else
        m.log.verbose("MOVING TO ROW ", m.itemScroller.index)
        ' m.moveToRow(m.itemScroller.index, false)
        '   ' m.focusIndicatorManager@.setFocusComplete()
        ' end if
        m.focusIndicatorManager.isHidden = m.top.isChildFocused or m.top.isFocused
      else
        m.log.verbose("LIST HAS NO COMPONENTS ")
        m.focusIndicatorManager.isHidden = true
      end if

      ' ? "FORCE UPDATE " ; m.focusedRowIndex ; " CELL " ; m.focusedCellIndex
      ' m.moveToRowItemIndex(m.focusedRowIndex, m.focusedCellIndex, false)
      ' m.moveToRowItemIndex(0, 0, true)
    end function

    function getRow(index as integer) as dynamic
      if m.contentManager.content <> invalid
        return m.contentManager.content@.getChild(index)
      else
        return invalid
      end if
    end function

    function getItem(rowIndex as integer, index as integer) as dynamic
      if m.contentManager.content <> invalid
        rowItem = m.contentManager.content@.getChild(rowIndex)
        if rowItem <> invalid
          return rowItem@.getChild(index)
        end if
      end if
      return invalid
    end function

    function moveToEnd(direction = -1 as integer, animated = false as boolean)
      'TODO get content index of first visible row
      if direction = -1
        rowIndex = 0
      else
        rowIndex = m.viewManager.numberOfItems
      end if

      animSpeedIndex = 0
      if m.viewManager.listSettings <> invalid
        if m.viewManager.listSettings.fastAnimateToEndEnabled
          animSpeedIndex = m.viewManager.listSettings.animSpeeds.count() - 1
        else
          animated = false
        end if
      end if
      m.moveToRow(rowIndex, animated, animSpeedIndex)
    end function

    function moveToRow(index as integer, animated as boolean, animSpeedIndex = 0 as integer)
      if m.contentManager.content <> invalid
        m.itemScroller.moveToContentIndex(index, animated = true, animSpeedIndex)
      end if
    end function

    function moveToRowItemIndex(rowIndex as integer, index as integer, animated as boolean, animSpeedIndex = 0 as integer) as void
      m.log.verbose(" MTRI  ", rowIndex, index, animated)
      m.log.increaseIndent()
      if m.contentManager.content <> invalid and rowIndex <> -1 and index <> -1
        row = m.getRowRenderer(rowIndex)
        m.log.verbose(" MTR A")
        if row <> invalid
          m.log.verbose(" MTR B")
          rowInfo = m.viewManager.getCellInfoForContentIndex(rowIndex)
          if rowInfo <> invalid
            rect = row@.getRect(index)
            subIndex = rowInfo.index
            if rect <> invalid
              subIndex += rect.subRowIndex
            end if
            m.moveToSubRowIndex(subIndex, animated = true, animSpeedIndex, false, false)
          end if
          row@.moveToIndex(index, false)
        end if
      end if
      m.log.decreaseIndent()
    end function

    function moveToSubRowIndex(index as integer, animated as boolean, animSpeedIndex = 0 as integer, setFocusIndex = false as boolean, useGridHack = false as boolean)
      if m.contentManager.content <> invalid and index <> -1 'and index <> m.focusedRowIndex
        m.itemScroller.disableNonAnimatedFocusUpdates = useGridHack
        m.itemScroller.moveToIndex(index, animated = true, animSpeedIndex)
        m.itemScroller.disableNonAnimatedFocusUpdates = false
        ' ? " MOVING TO SUB ROW INDEX " ; index
        compInfo = m.viewManager.cellInfos[index]
        if setFocusIndex and compInfo <> invalid
          m.focusedCellIndex = index
        end if
        ' m.itemScroller.forceFocusOnIndex(m.focusedRowIndex, true)
        ' ? "DONE"
      end if
    end function

    function setUpdatesEnabled(enabled as boolean)
      m.contentManager.setUpdatesEnabled(enabled)
    end function

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ Renderer Helpers
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    function getRowRenderer(index as integer)
      return m.viewManager.getRenderer(index)
    end function

    function getCellRenderer(rowIndex as integer, cellIndex as integer)
      row = m.getRowRenderer(rowIndex)

      if row <> invalid
        return row@.getRenderer(cellIndex)
      end if

      return invalid
    end function

    function getFocusedRow()
      if m.itemScroller.cellInfo <> invalid
        return m.itemScroller.cellInfo.component
      else
        return invalid
      end if
    end function

    function getFocusedCell()
      return m.getCellRenderer(m.focusedRowIndex, m.focusedCellIndex)
    end function

    function getFocusedRowContent()
      if m.contentManager.content <> invalid
        return m.contentManager.content@.getChild(m.focusedRowIndex)
      else
        return invalid
      end if
    end function

    function getFocusedCellContent()
      rowContent = m.getFocusedRowContent()
      if rowContent <> invalid
        return rowContent@.getChild(m.focusedRowIndex)
      else
        return invalid
      end if
    end function

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ Internal api
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    function _onContentItemChange(changes as mc.types.array)
      ' ? "______________________________ _onContentItemChange" ; m.__classname
      ' ? "GOT CHANGES" ; formatJson(mc.collections.map(changes, function(c as mc.types.assocarray)
      '   return c.type
      ' end function))
      m.log.verbose(" LIVM _onContentItemChange")
      m.viewManager.forceUpdateLayout()

      'TODO - need to ensure that the current focus and scroll position is valid after an update!
      m.itemScroller.processChanges(changes)
      '// cspell:disable-next-line
      ' ? "OCIC: INDEX " ; m.itemScroller.index ; "RI"; m.focusedRowIndex
      ' m.moveToRow(m.itemScroller.index, false)
      ' ? " contents changed - moving to row " ; m.focusedRowIndex ; " " ; m.focusedCellIndex
      ' m.moveToRowItemIndex(m.focusedRowIndex, m.focusedCellIndex, false)

      if m.top.isChildFocused or m.top.isFocused
        m.focusIndicatorManager@.setFocusedOnRow(true)
      end if
    end function

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ Focus indicator callbacks
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    function onFlowFocusFraction(fraction as float)
      m.itemScroller.onFlowFocusFraction(fraction)
    end function

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ Movement Methods
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    private function canMoveFromKeyPress(key as string) as boolean
      direction = m.keyDirections[key]

      if m.focusIndicatorManager.isFocusing
        'TODO - improve this
        m.log.info("ignoring press", key, "because the focus indiciator is moving")
        return false
      end if

      if m.listKeys[key] = true
        return m.itemScroller.canMoveInDirection(direction)
      else if m.rowKeys[key] = true
        row = m.getFocusedRow()
        if row <> invalid
          return row@.canMoveInDirection(direction)
        else
          m.log.verbose("FOCUSED ROW INVALID")
        end if
      end if
      return false
    end function

    private function onRowKeyPress(key as string) as boolean
      m.log.info("key", key)
      m.itemScroller.isDoingLongPress = false

      if m.isAlreadyScrolling(false)
        return true
      end if

      m.log.verbose(" OKP ", key)
      if m.canMoveFromKeyPress(key)
        direction = m.keyDirections[key]
        m.isDoingFlowFocus = false
        isPage = key = "rewind" or key = "fastforward"
        m.log.verbose(" FROM KEY PRESS ", key, " isPage ", isPage)
        return m.getFocusedRow()@.moveFromPress(direction, false, isPage)
      end if

      return false
    end function

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ KeyPress
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    private function onKeyPressFastForward() as boolean
      return m.onRowKeyPress("fastforward")
    end function

    private function onKeyPressRewind() as boolean
      return m.onRowKeyPress("rewind")
    end function

    private function onKeyPressLeft() as boolean
      return m.onRowKeyPress("left")
    end function

    private function onKeyPressRight() as boolean
      return m.onRowKeyPress("right")
    end function

    private function canScroll(key as string) as boolean
      if m.listKeys[key]
        if m.isAlreadyScrolling(m.listKeys[key] = true)
          return false
        end if
      else if m.rowKeys[key] = true
        direction = m.keyDirections[key]
        return m.getFocusedRow()@.canMoveInDirection(direction)
      end if
      return true
    end function

    private function isAlreadyScrolling(isUpDown as boolean) as boolean
      ' ? "VIEW MANAGER IS SCROLLING", m.viewManager.isScrolling ; " Doing FF " ; m.isDoingFlowFocus
      m.log.info("isAlreadyScrolling? ", m.focusIndicatorManager.isFocusing)

      return m.focusIndicatorManager.isFocusing or m.viewManager.isScrolling or (isUpDown and m.isDoingFlowFocus)
    end function

    private function onKeyPressOptions() as boolean
      m.log.info("updating list")

      if m._forceReloadListWhenPressingOption
        m.forceUpdate(true)
        return true
      end if

      return false
    end function

    private function onKeyPressUp() as boolean
      if not m.top.visible
        return false
      end if
      m.log.verbose("onKeyPressUp")

      'FIXME - add ability to change of direction, mid-animation
      ' isScrolling = m.viewManager.isScrolling or m.focusIndicatorManager.isDoingFlowFocus
      ' if isScrolling
      '   m.log.info("was already scrolling - ignoring")
      m.log.verbose(" CANNOT SCROLL is already scrolling ", m.viewManager.isScrolling, " FF ", m.focusIndicatorManager.isDoingFlowFocus)
      '   'TODO - improve this
      '   ' if m.focusIndicatorManager.isDoingFlowFocus
      '   '   return true
      '   ' end if
      '   m.itemScroller.move(1, false, true, true)

      '   return true
      ' end if

      if m.itemScroller.canMoveInDirection(-1)
        result = m.itemScroller.moveFromPress(-1, false)
        return result
      else
        return false
      end if
    end function

    private function onKeyPressOk() as boolean
      if not m.top.visible
        return false
      end if
      'FIXME - add ability to change of direction, mid-animation
      isScrolling = m.viewManager.isScrolling or m.focusIndicatorManager.isDoingFlowFocus or m.isScrolling
      if isScrolling
        m.log.info("Cannot select while scrolling")
        ' ? " CANNOT select while view is scrolling "; m.viewManager.isScrolling ; " FF " ; m.focusIndicatorManager.isDoingFlowFocus
        'TODO - improve this
        return true
      end if

      m.log.verbose("onKeyPressOk")
      if m.delegate <> invalid
        m.delegate@._apply("onItemSelected", [m.focusedRowIndex, m.focusedCellIndex], false)
      end if

      return true
    end function

    private function onKeyPressDown() as boolean
      if not m.top.visible
        return false
      end if
      m.log.verbose("onKeyPressDown")

      'FIXME - add ability to change of direction, mid-animation
      ' isScrolling = m.viewManager.isScrolling or m.focusIndicatorManager.isDoingFlowFocus
      ' ' if m.itemScroller.direction = 1 and isScrolling
      ' if isScrolling
      '   ' if false and isScrolling
      m.log.verbose(" CANNOT SCROLL is already scrolling ", m.viewManager.isScrolling, " FF ", m.focusIndicatorManager.isDoingFlowFocus)
      '   m.log.info("was already scrolling - ignoring")
      '   if m.focusIndicatorManager.isDoingFlowFocus
      '     return true
      '   end if
      ' end if

      if m.itemScroller.canMoveInDirection(1)
        ' if false and isScrolling
        '   m.itemScroller.cancelScrollingDuringPress()
        ' end if

        return m.itemScroller.moveFromPress(1, false)
      else
        m.log.verbose(" SKIPPING DOWN")
        return false
      end if
    end function

    private function getLongPressIntervalForKey(key as string) as float
      if not m.top.visible
        return 0
      end if
      m.log.verbose("getLongPressIntervalForKey", key)

      if m.canMoveFromKeyPress(key)
        return 0.2
      else
        return 0
      end if
    end function

    private function onLongPressStart(key as string) as boolean
      m.log.info(">>>>onLongPressStart", key)

      if m.canMoveFromKeyPress(key)
        direction = m.keyDirections[key]

        if m.listKeys[key] = true
          if m.itemScroller.canMoveInDirection(direction)
            m.log.verbose("long press started on key", key, "going to animate to start of list")
            return m.itemScroller.moveToEnd(direction)
          else
            m.log.verbose("long press started on key", key, "going to animate to end of list")
            return m.itemScroller.moveToEnd(1)
          end if
        else if m.rowKeys[key] = true
          m.log.verbose(" FROM LONG PRESS ", key)
          isPage = key = "rewind" or key = "fastforward"
          return m.getFocusedRow()@.moveFromPress(direction, true, isPage)
        end if
      end if

      return false
    end function

    private function onLongPressFinish(key as string) as boolean
      m.log.verbose(" ON LONG PRESS FINISH - LIST ", key)
      if m.listKeys[key] = true
        m.log.verbose(" OLF A")
        m.log.verbose("long press finished on key", key, "going to animate to final scroll position")
        m.itemScroller.cancelScrolling()
        return true
      else if m.rowKeys[key] = true
        m.log.verbose(" OLF B")
        m.getFocusedRow()@.cancelMove(false)
        return true
      end if

      return false
    end function

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ Lifecycle Methods
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    protected override function onGainedFocus(isSelfFocused as boolean)
      super.onGainedFocus(isSelfFocused)
      m.focusIndicatorManager.isHidden = false
      if isSelfFocused
        m.focusIndicatorManager@.setFocusedOnRow(true)
      end if
    end function

    protected override function onLostFocus()
      super.onLostFocus()
      'bs:disable-next-line
      if not m.top.isChildFocused
        m.focusIndicatorManager.isHidden = true
        m.focusIndicatorManager@.setFocusedOnRow(false)
      end if
    end function

    protected override function onChildLostFocus()
      'bs:disable-next-line
      if m.top.isChildFocused
        m.focusIndicatorManager.isHidden = false
        m.focusIndicatorManager@.setFocusedOnRow(true)
      else
        m.focusIndicatorManager.isHidden = true
        m.focusIndicatorManager@.setFocusedOnRow(false)
      end if
    end function
  end class
end namespace
