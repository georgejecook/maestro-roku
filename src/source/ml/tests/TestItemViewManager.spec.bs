
namespace tests
  @tags("ml")
  class TestItemViewManager extends ml.ItemViewManager
    public itemSize = [100, 100]
    public space = 0
    private comps

    function new(owner, container, axes, itemSize = [100, 100], space = 0)
      super("TestItemViewManager", owner, container, axes, 0, 1920)
      m.container = container
      m.owner = owner
      m.itemSize = itemSize
      m.space = space
      m.lookAheadDistance = 1500
      m.lookBehindDistance = 1500
    end function

    override function getRenderer(index)
      return m.visibleComponents[index]
    end function

    override function createRenderers(content as mc.types.node, forceSync = false as boolean) as boolean
      m.visibleComponents = []
      m.hiddenComponents = []
      m.comps = []
      m.rects = []

      x = 0
      y = 0
      if content <> invalid
        index = 0
        for each item in content.getChildren(-1, 0)
          comp = new rooibos.utils.MockNode(str(index).trim())
          comp.append({
            index: index
            content: item
            isRendered: false
            renderState: "none"
            _vs: "none"
            isInViewport: false
            visible: item.isHidden <> true
          })

          m.comps.push(comp)

          if comp.visible
            comp.translation = [x, y]

            'TODO better handle the whole visibility

            if m.axes = 0
              x += m.itemSize[0] + m.space
            else
              y += m.itemSize[1] + m.space
            end if

            m.rects.push(
            {
              translation: comp.translation
              size: m.itemSize
              index: index
            })
            m.visibleComponents.push(comp)
            m.visibleItems.push(comp.content)
          else
            m.hiddenComponents.push(comp)
          end if

          index++
        end for
      end if
      return true
    end function

    override function _updateLayout()
      m.log.info("_updateLayout")
      m.updateScreenRect()
      m.updateLayout()
    end function

    override function applyRenderStateMap(renderStateMap)
      ' ? "updating renderStateMap: " ; formatJson(renderStateMap)
      for each id in renderStateMap
        comp = m.comps[id.toInt()]

        if comp <> invalid
          if comp.renderState <> renderStateMap[id]
            comp.renderState = renderStateMap[id]
            comp.isRendered = comp.renderState = "rendered"
          end if
          if comp.isRendered
            m.renderedComponents.push(comp)
          end if
        else
          ' ? "ERROR could not find visible comp for "; id
        end if
      end for

    end function

    override function shouldUpdateRenderedIndexes(direction, index)
      return true
    end function

  end class

end namespace