import "pkg:/source/ml/ItemViewManager.bs"

namespace ml
  class RowItemViewManager extends ml.ItemViewManager
    public row = invalid
    public lastRowIsRendered = "none"
    public isFlowSupported = false

    function new(list as roSGNodeNode, row as roSGNodeNode, container as roSGNodeNode, componentPool as roSGNodeNode, minPos as integer, maxPos as integer)
      super("RowItemViewManager", list, container, componentPool, 0, minPos, maxPos)
      m.row = row
    end function

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ overridden
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    public override function reset()
      m.log.info("releasing", m.renderersByContentId.count(), "row children")
      for each id in m.renderersByContentId
        m.componentPool@.releaseComponent(m.renderersByContentId[id])
      end for
      m.renderersByContentId = {}
      super.reset()
    end function


    override function createRenderers(content as roSGNodeNode, forceSync = false as boolean) as boolean
      ' ? "::: CREATE RENDERERS" ; m.renderersByContentId
      'renderers are created as needed
      m.previousRendereredByContentId = m.renderersByContentId
      m.rendereredCellInfosByIndex = {}
      m.renderersByContentId = {}
      return true
    end function

    override function updateLayout(fullUpdate = false as boolean) as void
      ' ? " U A"
      m.log.info("ROW updateLayout")
      m.rects = []
      m.visibleItems = []
      m.cellInfos = []

      m.rowInfos = invalid
      if m.content = invalid
        return
      end if

      oldRenderersByContentId = m.renderersByContentId
      m.renderersByContentId = {}
      index = 0
      ' m.contentGroup.translation = [0, 0]
      focusSettings = m.settings.focusSettings
      x = 0
      for each item in m.content@.getChildren(-1, 0)
        ' ? " U B"
        ' ? ">>>> laying out ", item.title, index
        settings = ml.listMixin.getCellSettings(m.owner.content, m.content, item)
        xSpace = settings.space

        m.rects.push({
          translation: [x, 0]
          size: settings.size
          index: index
          rowIndex: m.row.index
          visible: true
        })
        maxPos = m.maxPos - settings.size[0] - xSpace

        positions = [
          new ml.CellPosition(x, 0) 'normal
          new ml.CellPosition(x, maxPos) 'float-scroll
        ]

        cellInfo = new ml.CellInfo(settings.size, focusSettings, positions, focusSettings.horizAnimStyle = "floating", index, index)
        cellInfo.index = index
        cellInfo.id = item.id
        m.cellInfos.push(cellInfo)

        'TODO - for now, no vis options for rows children..
        m.visibleItems.push(item)

        if oldRenderersByContentId.doesExist(item.id)
          m.renderersByContentId[item.id] = oldRenderersByContentId[item.id]
          oldRenderersByContentId.delete(item.id)
        end if
        x += settings.size[m.axes] + xSpace
        index++
      end for

      m.numberOfItems = m.visibleItems.count()
      m.numberOfVisibleItems = m.numberOfItems
      ' ? " U C NUM ITEMS NOW "; m.numberOfItems
      m.log.info("AFTER ROW UPDATE THESE RENDERERS ARE LEFT OVER", formatJson(oldRenderersByContentId.keys()))

      for each id in oldRenderersByContentId
        renderer = oldRenderersByContentId[id]
        m.log.info("cleanupCell OLD renderer", id, mc.dv(renderer))
        m.cleanupCell(renderer)
      end for
    end function

    override function onContentItemsChange(changes as roArray)
      m.log.info("updating row renderers due to contents change")
      oldNumComponents = m.numberOfItems
      previousInfo = ml.CellInfoUtils.cellInfoFromAA(m.itemScroller.cellInfo)
      m.forceUpdateLayout()

      result = {
        isLayoutRequired: false
        isFocusChangeRequired: false
      }

      if m.itemScroller.index >= m.content.length
        m.itemScroller.index = mc.clamp(m.itemScroller.index, 0, m.content.length - 1)
      end if

      m.rowInfos = invalid

      if (m.numberOfItems = 0 and oldNumComponents > 0) or (oldNumComponents = 0 and m.numberOfItems > 0)
        result.isLayoutRequired = true
      else
        m.itemScroller.notifyViewportEvents(m.itemScroller.direction, true)
      end if

      nextIndex = m.getIndexAfterItemUpdate(previousInfo)
      result.isFocusChangeRequired = nextIndex <> previousInfo.index
      m.itemScroller.moveToIndex(nextIndex, false)
      m.tidyUpUnusedRenderers()
      return result
    end function

  end class
end namespace
