import "pkg:/source/core/Utils.bs"
import "pkg:/source/view/NodeClass.bs"
import "pkg:/source/core/Command.bs"

namespace mc.internal.commands
  interface ICommandTask extends roSGNode
    __commandName as string
  end interface
end namespace

namespace mc
  @node("mc_CommandSequence", "Group")
  class CommandSequence extends mv.NodeClass

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ Events
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    public output as roAssociativeArray

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ Public Fields
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    @observer("begin")
    public control as string = invalid

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ Private Fields
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    private completedCommands = {}
    private runningCommands = {}
    private commands = {}
    private taskCommands = {}

    protected firstCommand as SequentialCommand

    function new()
      super("CommandSequence")
    end function

    private function begin()
      m.log.info("starting CommandSequence")

      m.firstCommand = new SequentialCommand("First Command", m.createCommands())
      m.startCommand(m.firstCommand as ICommand)
    end function

    protected function createCommands()
      m.log.error("override createCommands to create your command structure")
    end function

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ Loading command Management
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    protected function startCommand(command as ICommand) as void
      m.log.info("starting command", command.name)

      if command = invalid
        m.failCommand({ name: invalid } as ICommand, "received invalid command")
        return
      end if

      if command.isOptional = true and not command.isActive
        m.log.info("skipping inactive optional command", command.name)
        return
      end if

      if command.isProvider = true
        m.log.verbose("this command is a provider, using the provided command")
        providedCommand = mc.apply(command.func, [command], m as roAssociativeArray)
        if providedCommand = invalid
          m.log.warn("command", command.name, "did not provide a command. skipping")
          if command.parentCommand <> invalid
            m.finishCommand(command)
          end if
          return
        end if
        providedCommand.parentCommand = command.parentCommand
        command = providedCommand
      end if

      if m.completedCommands.doesExist(command.name) or m.runningCommands.doesExist(command.name)
        m.log.error("duplicate command name. Cannot proceed", command.name)
        m.failCommand(command, `Duplicate command name: ${command.name}`)
        return
      end if


      m.runningCommands[command.name] = true
      m.completedCommands[command.name] = false
      m.taskCommands[command.name] = command

      m.log.info("Starting command", command.name, "running:", m.getRunningCommandCount(), "running commands: ", formatJson(m.runningCommands.keys()))

      try
        if (command.isParallel = true or command.isSequence = true) and command.commandCount = 0
          m.log.warn("Parallel or sequential command, with no children:", command.name)
          m.finishCommand(command)

        else if command.isParallel = true
          m.log.verbose(`command ${command.name} is a parallel command - starting commands now`)
          m.startParallelCommands(command as ParallelCommand)

        else if command.isSequence = true
          m.log.verbose(`command ${command.name} is a sequential command - starting commands now`)
          m.startSequentialCommand(command as SequentialCommand)

        else if command.isTask = true
          task = m.createTask(command.taskName, command.taskArgs, m.onTaskCommandCallback, true, "both")
          if task <> invalid
            mc.setOrAddNodeField(task, "__commandName", command.name)
            if command.isWaitingResult = false
              m.log.verbose("this task command does not need to await the result to continue")
              m.finishCommand(command)
            end if
          else
            message = `Error executing command ${command.name} could not create task: ${command.taskName}`
            m.failCommand(command, message)
          end if
        else
          'bs:disable-next-line
          m.__cb = command.func
          'bs:disable-next-line
          m.__cb(command)
        end if
      catch error
        'bs:disable-next-line
        message = `Error executing command ${command.name} error was: ${formatJson(error)}`
        m.log.error(message)
        m.failCommand(command, message)
      end try
    end function

    private function onTaskCommandCallback(value as mc.Result, task as mc.internal.commands.ICommandTask)
      command = m.taskCommands[task.__commandName]
      if command <> invalid
        m.taskCommands.delete(task.__commandName)

        if command.onComplete <> invalid
          updatedOk = mc.apply(command.onComplete, [value], m as roAssociativeArray)
          isOk = asBoolean(updatedOk, asBoolean(value.isOk))
        else
          isOk = asBoolean(value.isOk)
        end if

        if command.isWaitingResult = false
          m.log.warn("this command was already completed, previously")
        else
          if isOk or asBoolean(command.ignoreFailure)
            m.finishCommand(command)
          else
            m.failCommand(command, `commandTask failed with error ${value.message}`)
          end if
        end if
      else
        m.log.error("Missing command on task completion call back", task.__commandName)
      end if
    end function

    protected function finishCommand(command as ICommand)

      if command.parentCommand <> invalid
        if command.parentCommand.isParallel = true
          m.finishParallelCommand(command as ParallelCommand)
        end if
        if command.parentCommand.isSequence = true
          m.finishSequentialCommand(command as SequentialCommand)
        end if

        if command.parentCommand.isFinished
          m.log.verbose("parent command", command.parentCommand.name, "is finished")
          m.finishCommand(command.parentCommand as ICommand)
        end if
      end if


      m.completedCommands[command.name] = true
      m.runningCommands.delete(command.name)
      m.log.info("Finished command", command.name, "running:", m.getRunningCommandCount(), "running commands: ", formatJson(m.runningCommands.keys()))
      command.isFinished = true
      command.timer.log("Finished")

      if m.getRunningCommandCount() = 0
        m.log.info("FINISHED WHOLE SEQUENCE")
        m.output = { isOk: true }
      end if
    end function

    function getRunningCommandCount()
      return m.runningCommands.keys().count()
    end function

    private function finishSequentialCommand(command as SequentialCommand)
      m.log.verbose("this command is a sequence, seeing if it has a next command")
      parentCommand = command.parentCommand as SequentialCommand
      parentCommand.moveNext()

      if parentCommand.currentCommand <> invalid
        m.log.verbose("has next command", parentCommand.currentCommand.name, "starting it")
        m.startCommand(parentCommand.currentCommand as ICommand)
      end if
      parentCommand.completeCommand(command)
    end function

    private function finishParallelCommand(command as ParallelCommand)
      m.log.verbose("this command is a parallel command, seeing if all commands are now finished")
      (command.parentCommand as ParallelCommand).completeCommand(command)
    end function

    private function startSequentialCommand(command as SequentialCommand)
      command.moveNext()
      if command.isFinished
        m.log.verbose("parent command is finished on very first task")
        m.finishCommand(command)
      else
        m.log.verbose("starting first command in sequence")
        m.startCommand(command.currentCommand)
      end if
    end function

    private function startParallelCommands(command as mc.ParallelCommand)
      for each c in command.childCommands
        if c <> invalid
          m.startCommand(c)
        end if
      end for
    end function

    protected function failCommand(command as ICommand, message as string)
      m.log.error(`command ${command.name} failed with message: `, message)
      if command.ignoreFailure = true
        m.log.warn("Ignoring failure")
        'FIXME should work!
        m.finishCommand(command as ICommand)
      else
        m.log.error("cannot continue from command failure")

        m.output = { isOk: false, message: message }
      end if
    end function

  end class
end namespace
