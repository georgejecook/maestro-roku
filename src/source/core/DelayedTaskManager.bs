import "pkg:/source/core/Tasks.bs"
import "pkg:/source/core/Utils.bs"
import "pkg:/source/core/BaseClass.bs"
import "pkg:/source/view/NodeClass.bs"

namespace mc.tasks

  ' /**
  '  * @module DelayedTaskManager
  '  * @description allows certain tasks to be scheduled at a certain time. Useful for things such as refreshing tokens, etc
  '  */
  @node("mc_DelayedTaskManager", "Group")
  class DelayedTaskManager extends mv.NodeClass

    private activeTimers = {}
    private delayedTasks = {}

    function new()
      super("DelayedTaskManager")
    end function

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ Public Methods
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    function cancelDelayedTaskWithId(id as string)
      m.log.info("requesting cancel of delayed task with id:", id)

      m.removeTimer(id)
      m.removeTask(id)
    end function

    function scheduleDelayedTask(nodeType as string, id as string, delay as float, fields as roAssociativeArray, resultField = "output" as string)
      m.log.info("creating delayed task of type", nodeType, "id", id, "delay", delay)

      if m.activeTimers[id] <> invalid or m.delayedTasks[id] <> invalid
        m.cancelDelayedTaskWithId(id)
      end if

      m.activeTimers[id] = m.createTimer(nodeType, id, delay, asAA(fields), resultField)
    end function

    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
    '++ Private Methods
    '+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

    private function createTimer(nodeType as string, id as string, delay as float, fields as roAssociativeArray, resultField = "output" as string)
      (fields as dynamic).id = id

      timer = m.setTimeout(m.onDelayedTaskTimerFire, delay, "node")
      mc.setOrAddNodeFields(timer as roSGNode, {
        id: id
        taskInfo: {
          fields: fields
          nodeType: nodeType
          resultField: resultField
        }
      })
      return timer
    end function

    private function removeTimer(id as string)
      timer = m.activeTimers[id]
      if timer <> invalid
        m.log.info("found timer - cancelling")
        m.cancelTimeout(timer)
        m.activeTimers.delete(id)
      end if
    end function

    private function removeTask(id as string)
      task = m.delayedTasks[id]
      if task <> invalid
        m.log.info("a task for this timer was already in flight: cancelling it now")
        m.cancelTask(task)
        if task.getParent() <> invalid
          task.getParent().removeChild(task)
        end if
        m.delayedTasks.delete(id)
      end if
    end function

    private function onDelayedTaskTimerFire(timer as roSGNode)
      'bs:disable-next-line
      taskInfo = timer.taskInfo as dynamic
      if taskInfo <> invalid
        m.activeTimers.delete(timer.id)
        m.delayedTasks[timer.id] = m.createTask(taskInfo.nodeType, taskInfo.fields)
      else
        m.log.error("ignoring unknown timer", timer.id, "it had no task info")
      end if
    end function

  end class
end namespace
