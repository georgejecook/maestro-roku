

function init()
  m.isManagingVisibilityCallbacks = false
end function

' /**
'  * @module BaseScreen
'  * @description Base view for all screens.
'     provides common functions, which all screens require
'  */

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'++ nav support
'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

function push(view as roSGNode, animated = true as boolean)
  navController = getNavController()

  if navController <> invalid
    navController@.push(view, animated)
  else
    m.log.error("Push failed - there is no navcontroller on ", m.top)
  end if
end function

function pop(animated = true as boolean) as object
  navController = getNavController()

  if navController <> invalid
    return navController@.pop(animated)
  else
    m.log.error("Pop failed - there is no navcontroller on ", m.top)
    return invalid
  end if
end function

function replaceNavControllerScreen(targetScreen as roSGNode, animated = false as boolean)
  navController = getNavController()

  if navController <> invalid
    navController@.replace(targetScreen, animated)
  else
    m.log.error("Pop failed - there is no navcontroller on ", m.top)
  end if
end function


function resetNavControllerToRoot(animated = false as boolean)
  m.log.method("resetNavControllerToRoot." + m.top.id)
  resetNavController(invalid, 0, animated)
end function

function resetNavController(newFirstScreen = invalid as roSGNode, endIndex = -1 as integer, animated = false as boolean)
  m.log.method("resetNavController." + m.top.id)
  navController = getNavController()

  if navController <> invalid
    navController@.reset(newFirstScreen, endIndex, animated)
  else
    m.log.error("Pop failed - there is no navcontroller on ", m.top)
  end if
end function

function getNavController()
  if m.top.navController <> invalid
    return m.top.navController
  else
    'might be nested in a screen; ultimately, there has to be a navcontroller somewhere
    return mv.getFirstParentWithType(m.top, "NavController")
  end if
end function

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'++ Screen helpers
'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

' /**
'  * @member _getTopScreen
'  * @memberof module:BaseScreen
'  * @instance
'  * @function
'  * @description gets the screen at the top of this screen's sceneGraph.
'  * @returns {BaseScreen} a screen considered as the top of this graph
'  */
function _getTopScreen(args = invalid as roAssociativeArray)
  m.log.method("_getTopScreen")
  topScreen = getTopScreen()
  navController = getNavController()
  if topScreen = invalid
    if navController <> invalid and navController.numberOfViews > 0
      topScreen = navController.currentView
    else
      topScreen = m.top
    end if
  end if
  return topScreen
end function

' /**
'  * @member getTopScreen
'  * @memberof module:BaseScreen
'  * @instance
'  * @function
'  * @description override point, for a screen to provide it's own means
'  * of looking up a screen at the top of it's stack
'  * @param {paramType} paramDescription
'  * @returns {returnType} returnDescription
'  */
function getTopScreen()
  return invalid
end function
