function init()
  m.log.info("init")
  m.viewStack = []
  m.currentView = invalid
  m.tempTransition = false
end function

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'++ public api
'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

function push(view as roSGNodemv_BaseScreen, animated = true as boolean, transition = invalid as roSGNode) as void
  m.log.method("push")
  if view = invalid
    m.log.warn(" push invalid view passed in : ignoring")
    return
  end if
  m.top.shouldRemoveFromAggregateView = false
  m.tempTransition = transition

  m.log.info("pushing ", view.subType())
  m.viewStack.push(view)
  view.navController = m.top
  updatePublicFields()

  'for a navcontroller we are added here
  m.top.pushedView = view
  transitionToView(view, animated)
end function

function reset(newFirstScreen = invalid as roSGNodemv_BaseScreen, endIndex = -1 as integer, animated = false as boolean, transition = invalid as roSGNode)
  m.log.info(" reset ", m.top.numberOfViews)
  if endIndex < -1
    endIndex = -1
  end if

  m.tempTransition = transition

  m.log.info("endIndex is", endIndex)
  index = m.top.numberOfViews - 1
  while index > endIndex
    m.log.info("resetting index ", index)
    view = m.viewStack.Pop()
    if view <> invalid
      hideView(view)
      view.navController = invalid
      view@.onRemovedFromAggregateView(m.top)
    else
      m.log.info(" reset found invalid child")
    end if
    index--
  end while

  updatePublicFields()

  if newFirstScreen <> invalid
    m.log.info("new first screen ", newFirstScreen.subType())
    push(newFirstScreen, animated)
  else if m.top.numberOfViews > 0
    m.log.info("there were views left on the stack after resetting ")
    transitionToView(m.viewStack[m.top.numberOfViews - 1], animated)
  end if


end function

function pop(animated = true as boolean, transition = invalid as roSGNode)
  m.log.method("pop ", m.top.numberOfViews)
  previousView = m.top.currentView
  m.tempTransition = transition

  nextView = invalid

  if previousView <> invalid
    m.top.poppedView = m.viewStack.pop()
    previousView.navController = invalid

    nextView = m.viewStack.Peek()
    m.top.pushedView = nextView
  end if

  updatePublicFields()

  m.top.shouldRemoveFromAggregateView = true
  transitionToView(nextView, animated)

  return previousView
end function

function replace(newView = invalid as roSGNodemv_BaseScreen, animated = false as boolean, transition = invalid as roSGNode) as object
  m.log.method("replace ", m.top.numberOfViews)
  previousView = m.top.currentView
  m.tempTransition = transition

  if previousView <> invalid
    m.viewStack.Pop()
    previousView.navController = invalid
  end if

  if newView <> invalid
    m.log.info("replacing with ", newView.subType())
    m.viewStack.Push(newView)
    newView.navController = m.top
  else
    m.log.warn("replace invalid view passed in : ignoring")
  end if

  updatePublicFields()

  transitionToView(newView, animated)

  return previousView
end function

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'++ Private impl
'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

function updatePublicFields()
  m.top.numberOfViews = m.viewStack.count()
  m.top.viewStack = m.viewStack

  if m.top.numberOfViews = 0
    m.top.isLastViewPopped = true
  else
    m.top.isLastViewPopped = false
  end if
end function

'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
'++ Lifecycle methods
'+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

function initialize(args as roAssociativeArray)
  m.log.method("initialize(args)")
  m.log = new rlog.Logger("NC.(" + m.top.getParent().subType() + ")")
end function

